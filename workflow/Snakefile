"""
NetInfer: A Snakemake pipeline for microbiome network inference
"""
# netinfer --input test/data/abundance.tsv --output test/testout --taxonomy test/data/taxonomy.tsv --threads 6 --config config/config.yaml --snake_args="--unlock"

import os
import re
import sys
import platform
from snakemake.utils import min_version

# Set minimum Snakemake version
min_version("9.0.0")

# Load configuration
configfile: "config/config.yaml"

# Include rules
include: "rules/preprocessing.smk"
include: "rules/flashweave.smk"
include: "rules/flashweaveHE.smk"
include: "rules/fastspar.smk"
include: "rules/spearman.smk"
include: "rules/spieceasi.smk"
include: "rules/propr.smk"
include: "rules/jaccard.smk"
include: "rules/aggregation.smk"
include: "rules/visualization.smk"

# Get output directory from config and ensure no whitespace
outdir = config.get("output_dir", "results").strip()  # Default to "results" if not specified

# Optional suffix for final aggregated outputs
suffix = str(config.get("suffix", "")).strip()
suffix_tag = f"-{suffix}" if suffix else ""

# Define final output files
final_outputs = [
    f"{outdir}/networks/merged_edges{suffix_tag}.tsv",
    f"{outdir}/networks/merged_network{suffix_tag}.gml",
    f"{outdir}/networks/network_info{suffix_tag}.json"
]

# Add visualization output if enabled
if config.get("visualization", {}).get("enabled", True):
    final_outputs.append(f"{outdir}/visualization/network_viewer.html")

# Add method-specific outputs based on config
if config["fastspar"]["enabled"]:
    final_outputs.append(f"{outdir}/networks/fastspar/network.tsv")
if config["spearman"]["enabled"]:
    final_outputs.append(f"{outdir}/networks/spearman/network.tsv")
if config["spieceasi"]["enabled"]:
    final_outputs.append(f"{outdir}/networks/spieceasi/network.tsv")
if config["propr"]["enabled"]:
    final_outputs.append(f"{outdir}/networks/propr/network.tsv")
if config["jaccard"]["enabled"]:
    final_outputs.append(f"{outdir}/networks/jaccard/network.tsv")
if config["flashweave"]["enabled"]:
    final_outputs.append(f"{outdir}/networks/flashweave/normal/network.tsv")
if config["flashweaveHE"]["enabled"]:
    final_outputs.append(f"{outdir}/networks/flashweave/HE/network.tsv")

rule all:
    input:
        final_outputs